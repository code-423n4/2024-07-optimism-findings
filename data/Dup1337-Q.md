|         | Issues                                                                                    |
| ------- |:----------------------------------------------------------------------------------------- |
| [L-01]  | Malicious party can prolong dispute for long time |
| [L-02]  | Withdrawals can't be performed if withdrawal is posted on-chain out-of-order |
| [L-03]  | `parentIndex` is not big enough to hold all the possible positions |

## [L-01] Malicious party can prolong dispute for long time
`MAX_CLOCK_DURATION` was introduced to counter a freeloader claim, giving the opposing party enough time to to respond. In works by changing clock duration to `MAX_CLOCK_DURATION - CLOCK_EXTENSION` most of the times:
```javascript
        if (nextDuration.raw() > MAX_CLOCK_DURATION.raw() - CLOCK_EXTENSION.raw()) {
            // If the potential grandchild is an execution trace bisection root, double the clock extension.
            uint64 extensionPeriod =
                nextPositionDepth == SPLIT_DEPTH - 1 ? CLOCK_EXTENSION.raw() * 2 : CLOCK_EXTENSION.raw();
            nextDuration = Duration.wrap(MAX_CLOCK_DURATION.raw() - extensionPeriod);
        }
```

The problem is that `CLOCK_EXTENSION` period is active every time, allowing malicious party to extend their clock multiple times. In current mainnet setup, where max depth is 73 and clock extension is set to 3 hours, the malicious party can extend the dispute to around 3.5 days + 71 * 3 hours, giving 297 hours => over `12 days`. Because whole claim can't be resolved untill all subgame claims are resolved, malicious actor can extend the wirhtdrawal, temporarily DoSing the withdrawal.

Below is PoC showing that you can easily go over 3.5 days of clock time:

```javascript
    function test_del_gameChallengeDuration() public {
        uint24 [9] memory durations = [
            3.5 days - 50 minutes,
            3 hours,
            1 hours,
            2 hours,
            2 hours,
            1 hours,
            2 hours,
            2 hours,
            3 hours
        ];

        uint256 duration1 = 3.5 days - 50 minutes;
        uint256 duration2 = 3 hours;
        uint256 duration3 = 1 hours;
        uint256 numNodes = durations.length;

        assertEq(gameProxy.maxClockDuration().raw(), 3.5 days);

        Claim challengedClaim = gameProxy.rootClaim();

        uint256 currentTime = block.timestamp;
        uint256 startingTime = block.timestamp;

        vm.warp(currentTime = block.timestamp + durations[0]);
        assertEq(currentTime, startingTime + durations[0]);

        for(uint i = 1; i < numNodes; i++) {
            console.log("t", i);
            uint requiredBond = gameProxy.getRequiredBond(LibPosition.wrap(uint8(i), 0));
            gameProxy.move{value: requiredBond}(challengedClaim, i - 1, Claim.wrap(bytes32(uint(0x01))), true);


            vm.warp(currentTime = block.timestamp + durations[i]);
            // assertEq(currentTime, startingTime + duration1 + duration2);
            (,,,,challengedClaim,,) = gameProxy.claimData(i);
        }

        console.log("Iterating over durations");

        for (uint i; i < numNodes; i++) {
            console.log("Node ", i);
            Duration nextDuration = gameProxy.getChallengerDuration(i);
            (,,,,,,Clock clock) = gameProxy.claimData(i);

            console.log("challenger duration: %s ; hours: %s", nextDuration.raw(), nextDuration.raw() / 1 hours);
            console.log("clock duration: %s ; hours: %s", clock.duration().raw(), clock.duration().raw() / 1 hours);
        }

    }
```

## [L-02] Withdrawals can't be performed if withdrawal is posted on-chain out-of-order
Anchor state is updated each time when fault dispute game is resolved:

```javascript
src/dispute/FaultDisputeGame.sol
    function resolve() external returns (GameStatus status_) {
        // [...]

        // Try to update the anchor state, this should not revert.
@>      ANCHOR_STATE_REGISTRY.tryUpdateAnchorState();
    }
```

```javascript
src/dispute/AnchorStateRegistry.sol
    function tryUpdateAnchorState() external {
        // [...]

        // Actually update the anchor state.
@>      anchors[gameType] = OutputRoot({ l2BlockNumber: game.l2BlockNumber(), root: Hash.wrap(game.rootClaim().raw()) });
    }
```

Such an anchor is then used when creating new dispute, to verify if the L2 block number it references is not older than anchored block:

```javascript
src/dispute/FaultDisputeGame.sol
    function initialize() public payable virtual {
        // [...]
        // Grab the latest anchor root.
@>      (Hash root, uint256 rootBlockNumber) = ANCHOR_STATE_REGISTRY.anchors(GAME_TYPE);
        // [...]
        // Do not allow the game to be initialized if the root claim corresponds to a block at or before the
        // configured starting block number.
@>      if (l2BlockNumber() <= rootBlockNumber) revert UnexpectedRootClaim(rootClaim());
```

Under normal circumstances it's not an issue, because the clock has to expire on all of the subgames in order to resolve game and update state anchor for a game (min 3.5 days according to current mainnet settings), however in such cases:

1. black swan event, making it impossible to put new withdrawals for extended period of time
2. bug introduced in one of the game types, allowing to resolve game immediately

It might be actually be possible to make withdrawals, which were created before the one that was settled in the registry, but not yet created on L1.

We recommend adding additional safeguards to StateAnchorRegistry, to prevent against such issues.

## [L-03] `parentIndex` is not big enough to hold all the possible positions

`parentIndex` type size might not be enough to hold all indexes. Technically the biggest position depth is 127, on mainnet it's set to 73, so it's way above 2^32. During initialization of a dispute, root node parent index is set to this number (2^32 - 1):
```javascript
    function initialize() public payable virtual {
//[...]
             // Set the root claim
        claimData.push(
            ClaimData({
                // @ok why max? is u32 enough?
                parentIndex: type(uint32).max,
                counteredBy: address(0),
                claimant: gameCreator(),
                // @ok are they checking for msg.value here?
                bond: uint128(msg.value),
                claim: rootClaim(),
                position: ROOT_POSITION,
                // @ok check inputs for this wrap
                clock: LibClock.wrap(Duration.wrap(0), Timestamp.wrap(uint64(block.timestamp)))
            })
        );
```

```
➜ 1 << 73 > type(uint32).max
Type: bool
└ Value: true
```

This is problem when getting challenger move duration:

```javascript
    function getChallengerDuration(uint256 _claimIndex) public view returns (Duration duration_) {
        //[...]
@>      if (subgameRootClaim.parentIndex != type(uint32).max) {
            parentClock = claimData[subgameRootClaim.parentIndex].clock;
        }
```

In the code snippet above, when parent index is `uint32.max`, parent clock is not used, because it's assumed that only root claim might have this value.


When it might happen:
1. achieving `uint32.max` number of claims.
2. Any issue allowing to override `claimData` storage variable length storage slot.


### Mitigation
For `getChallengerDuration()`, consider changing the check to the following:

```diff
-       if (subgameRootClaim.parentIndex != type(uint32).max) {
+       if (_claimIndex != 0) {
            parentClock = claimData[subgameRootClaim.parentIndex].clock;
        }
```

